# Contributor: Slash <demodevil5 [at] yahoo [dot] com>

pkgname=hon
pkgver=1.0.0.1
pkgrel=1
pkgdesc="Heroes of Newerth is a Real Time Strategy game heavily influcenced by DotA"
arch=('i686' 'x86_64')
url="http://www.heroesofnewerth.com/"
license=('custom:HoN')
depends=('libgl' 'libxml2' 'mesa')
makedepends=('unzip')
install='hon.install'
source=('hon.desktop' 'hon.launcher' \
"http://dl2.heroesofnewerth.com/HoNClient-${pkgver}.sh")
md5sums=('d3fa56f17b5eb21d4da01e8d628fbc97'
         '4e104d5954ec711c0b89e9f968917758'
         '23ba0179d530a582280155bc75c3af2d')

build() {
    # Extract Files
    unzip $srcdir/HoNClient-${pkgver}.sh

    # Create Destination Directory
    install -d $pkgdir/opt/hon/

    # Install Game & Data Files
    cp -r $srcdir/data/* \
        $pkgdir/opt/hon/

    # Install License (HoN TOS)
    install -D -m 0644 $srcdir/data/tos.txt \
        $pkgdir/usr/share/licenses/$pkgname/hon_tos.txt

    # Install License (S2 TOS)
    install -D -m 0644 $srcdir/meta/license.txt \
        $pkgdir/usr/share/licenses/$pkgname/hon_license.txt

    # Install Desktop Shortcut
    install -D -m 0644 $srcdir/hon.desktop \
        $pkgdir/usr/share/applications/hon.desktop

    # Install Client Launcher
    install -D -m 0755 $srcdir/hon.launcher \
        $pkgdir/usr/bin/hon

    # Set Ownership to root:games 
    chown -R root:games $pkgdir/opt/hon
    chmod -R g+w $pkgdir/opt/hon
}


# Contributor: Slash <demodevil5[at]yahoo[dot]com>

pkgname=teamspeak3
pkgver=beta22
pkgrel=1
pkgdesc="TeamSpeak is software for quality voice communication via the Internet"
url="http://www.teamspeak.com/"
license=('custom')
depends=('libpng12' 'qt')
arch=('i686' 'x86_64')
source=("http://ftp.4players.de/pub/hosted/ts3/releases/beta-${pkgver:4:2}/TeamSpeak3-Client-linux_x86-3.0.0-${pkgver}.run" \
'teamspeak3.desktop' 'icon.xpm' 'teamspeak3.launcher')
md5sums=('3187e38943f0e9d6ecb9ab76bccf714a'
         'be58a8798c5d1b7e95a89f6a6482db3e'
         '19f103384e4c1a0aafc8fed44de0c87b'
         '170fcbb1b468ce4f3dba7ee3fb2a1c6d')
_TSARCH='x86'

if [ "$CARCH" == "x86_64" ]; then
    source[0]="http://ftp.4players.de/pub/hosted/ts3/releases/beta-${pkgver:4:2}/TeamSpeak3-Client-linux_amd64-3.0.0-${pkgver}.run"
    md5sums[0]='e73c1f50b8c209a40c690f4fac0b6274'
    _TSARCH='amd64'
fi

build() {
    cd $srcdir

    # Create Destination Directories
    install -d $pkgdir/{usr/bin/,opt/}

    # Make Installer Executable
    chmod +x TeamSpeak3-Client-linux_${_TSARCH}-3.0.0-${pkgver}.run

    # Run Installer
    ./TeamSpeak3-Client-linux_${_TSARCH}-3.0.0-${pkgver}.run --noexec --target teamspeak3

    # Move Program Data to Package Directory
    mv $srcdir/teamspeak3/ $pkgdir/opt/

    # Install Desktop File
    install -D -m644 $srcdir/teamspeak3.desktop \
        $pkgdir/usr/share/applications/teamspeak3.desktop

    # Install Icon File
    install -D -m644 $srcdir/icon.xpm \
        $pkgdir/usr/share/pixmaps/teamspeak3.xpm

    # Install Custom License
    install -D -m644 $pkgdir/opt/teamspeak3/LICENSE \
        $pkgdir/usr/share/licenses/$pkgname/LICENSE

    # Install Client Launcher
    install -D -m755 $srcdir/teamspeak3.launcher \
        $pkgdir/usr/bin/teamspeak3
}

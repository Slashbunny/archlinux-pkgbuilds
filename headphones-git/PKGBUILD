# Contributor: Slash <demodevil5 at yahoo dot com>
# Contributor: blades <blades at gecko dot org dot uk>

pkgname=headphones-git
pkgver=20120920
pkgrel=1
pkgdesc="Headphones is an automated NZB downloader, similar to Sick Beard and CouchPotato, but for music!"
arch=('any')
url="http://github.com/rembo10/headphones"
license=('GPL3')
depends=('python2')
makedepends=('git')
backup=('etc/conf.d/headphones')
install='headphones.install'
source=('headphones.sh' 'headphones.rc' 'headphones.confd')
md5sums=('87371d36fa0f02e32b313931b8c15f8c'
         '69ce140837d89bd61c47270840c47e5b'
         '9dfe8da3a102328d45438f3f09eefc5c')

_gitroot=git://github.com/rembo10/headphones.git
_gitname=headphones

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting build..."
}

package() {
    # Make destination directories
    install -d ${pkgdir}/{opt/,var/run/headphones}

    # Move Maraschino files to destintion directory
    cp -r ${srcdir}/${_gitname} ${pkgdir}/opt/headphones

    # Install Launcher Script
    install -D -m755 "${srcdir}/headphones.sh" \
        "${pkgdir}/usr/bin/headphones"

    # Install Daemon Script
    install -D -m755 "${srcdir}/headphones.rc" \
        "${pkgdir}/etc/rc.d/headphones"

    # Install Daemon Configuration
    install -D -m644 "${srcdir}/headphones.confd" \
        "${pkgdir}/etc/conf.d/headphones"
}


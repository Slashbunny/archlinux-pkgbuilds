# Contributor: Slash <demodevil5[at]yahoo[dot]com>

pkgname=mpd-git
pkgver=20120303
pkgrel=1
pkgdesc="Music daemon that plays MP3, FLAC, and Ogg Vorbis files"
url="http://mpd.wikia.com/wiki/Server"
depends=(
    'alsa-lib' 'audiofile' 'curl' 'faad2' 'ffmpeg' 'flac' 'glib2'
    'libcdio' 'libgme' 'libid3tag' 'libmad' 'libmikmod' 'libmodplug' 'libmpcdec' 'libpulse'
    'libsamplerate' 'libvorbis' 'libid3tag' 'libshout' 'mpg123' 'wavpack' 'zziplib')
makedepends=('git')
optdepends=(
    'avahi: Support for Avahi Zeroconf Backend'
    'jack-audio-connection-kit: Support for JACK Audio'
    'lame: Required to serve mp3 streams (or use twolame)'
    'libao: Audio Library not recommended unless OSS/ALSA does not work'
    'libao-pulse: Support for the PulseAudio Sound Server (if using libao)'
    'libcue: Support for embedded cuesheets'
    'libmms: Support for MMS Protocol'
    'pulseaudio: Support for the PulseAudio Sound Server'
    'sqlite3: Support for SQLite Database'
    'twolame: Required to serve mp3 streams (or use lame)'
)
license=('GPL')
arch=('i686' 'x86_64')
conflicts=('mpd')
provides=('mpd')
replaces=('mpd-svn')
install=mpd.install
source=('mpd.rc')
md5sums=('e5669c2bff4031928531e52475addeb1')

_gitroot="git://git.musicpd.org/master/mpd.git"
_gitname="mpd"

build() {
    cd $srcdir

    msg "Connecting to Git server..."

    if [ -d $srcdir/$_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot
    fi

    msg "Git checkout done or server timeout."
    msg "Starting make..."

    # Copy Latest files to Build Directory
    cp -r $srcdir/$_gitname $srcdir/$_gitname-build
    cd $srcdir/$_gitname-build

    # Configure Source
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-ao \
        --disable-libwrap \
        --disable-jack \
        --disable-soundcloud \
        --enable-bzip2 \
        --enable-httpd-output \
        --enable-iso9660 \
        --enable-lastfm \
        --enable-modplug \
        --enable-pulse \
        --enable-zzip \
        --with-zeroconf=no
        #--enable-cue \
        #--enable-sqlite \
        #--enable-mms \
        #--enable-mvp \

    # Build Source
    make
}

package() {
    cd $srcdir/$_gitname-build

    # Install Source
    make prefix=$pkgdir/usr install

    # Remove Build Directory
    rm -r $srcdir/$_gitname-build/

    # Create Destination Directories
    install -d $pkgdir/var/{log/mpd,lib/mpd/playlists}

    # Install Init Script
    install -D -m755 $srcdir/mpd.rc \
        $pkgdir/etc/rc.d/mpd

    # Install Sample Config
    install -D -m644 $srcdir/mpd/doc/mpdconf.example \
        $pkgdir/usr/share/mpd/mpd.conf.example

    # Modify Sample Config with Proper Directories and User Settings
    /bin/sed -i 's|music_directory.*$|#music_directory "path_to_your_music_collection"|1' $pkgdir/usr/share/mpd/mpd.conf.example
    /bin/sed -i 's|playlist_directory.*$|playlist_directory "/var/lib/mpd/playlists"|1'   $pkgdir/usr/share/mpd/mpd.conf.example
    /bin/sed -i 's|db_file.*$|db_file "/var/lib/mpd/mpd.db"|1'                            $pkgdir/usr/share/mpd/mpd.conf.example
    /bin/sed -i 's|log_file.*$|log_file "/var/log/mpd/mpd.log"|1'                         $pkgdir/usr/share/mpd/mpd.conf.example
    /bin/sed -i 's|error_file.*$|error_file "/var/log/mpd/mpd.error"|1'                   $pkgdir/usr/share/mpd/mpd.conf.example
    /bin/sed -i 's|#pid_file.*$|pid_file "/var/run/mpd/mpd.pid"|1'                        $pkgdir/usr/share/mpd/mpd.conf.example
    /bin/sed -i 's|#state_file.*$|state_file "/var/lib/mpd/mpdstate"|1'                   $pkgdir/usr/share/mpd/mpd.conf.example
    /bin/sed -i 's|#user.*$|user "mpd"|1'                                                 $pkgdir/usr/share/mpd/mpd.conf.example
}

